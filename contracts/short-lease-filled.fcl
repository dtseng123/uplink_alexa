global account renter = '55MJJE7u5kcbhgdjr8tWbvmcQWCq2xMqjYHsV4ovjUft';
global account landlord = '12pk9kCSAAoSkXmZRWUPqxR47hXm2ZHVpNtyqhrisLNb';
global asset asset_ = '7DJKgrCn4zbymmMEsSVqcW6S44AdBX2RsHFcG3MojX6y';
global int rent_amount_total = 1000;
global int deposit = 200;

global datetime check_in_date = "2018-01-01T11:56:51+00:00";
global datetime check_out_date = "2018-01-01T11:56:51+00:00";

transition initial -> terminal;
transition lease_confirmation -> terminal;
transition lease_check_out -> terminal;
transition damage_check -> terminal;

transition initial -> lease_confirmation;
transition lease_check_in -> lease_confirmation;
transition lease_confirmation -> lease_check_out;
transition lease_checkout -> damage_check;


@initial
lease_check_in() {
  if ((sender() == renter)) {
    after (check_in_date){
      transferTo(asset_, deposit);
      transitionTo(:lease_confirmation);
    };
  };
}


@lease_confirmation
lease_confirmation() {
  if ((sender() == landlord)) {
      transferHoldings(renter, asset_, rent_amount_total, landlord);
      transitionTo(:lease_check_out);
  };
}


@lease_check_out
lease_check_out() {
  if ((sender() == renter)) {
      after (check_out_date) {
        transitionTo(:damage_check);
      };
  };
}


@damage_check
damage_check(bool damages, int damages_amt) {
  if ((sender() == landlord)) {
      if(damages == False){
        transferFrom(asset_, deposit, renter);
        transitionTo(:terminal);
      };
      if(damages == True) {
        if(damages_amt <= deposit) {
          transferFrom(asset_, damages_amt, landlord);
          transferFrom(asset_, (deposit - damages_amt), renter);
          transitionTo(:terminal);
        };
        if(damages_amt > deposit) {
          transferFrom(asset_, deposit, landlord);
          return("Damages exceed security deposit amount, please file a civil claim for remaining amount");
          transitionTo(:terminal);
        };
      };
  };
}